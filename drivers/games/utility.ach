# UTILITY.ACH
#
# Generally useful class definitions.

# scenery:  these are objects which are really part of the surroundings,
# but will yield more information on a closer look.

type scenery based on object

  first_seen : TRUE
  visible    : FALSE

methods

  'get' : >>I can't do that.
  'look' : {
    message --> object
    first_seen := FALSE
    }

end


Verb null

full : 'open'
  normal : TRUE

methods

'NORMAL' :
    if main.subj.open = UNDEFINED then
      ABSENT
    else if main.subj.open then
      write "It's already open."
    else {
      write main.voice.Who & " opened ", 'DEF' -> main.subj, "."
      main.subj.open := TRUE
      }
end

Verb null

  full : 'close'
  syn : 'shut'

methods

'NORMAL' :
    if main.subj.open = UNDEFINED then
      ABSENT
    else if not main.subj.open then
      write "It's already closed."
    else {
      write main.voice.Who & " closed ", 'DEF' -> main.subj, "."
      main.subj.open := FALSE
      }
end


type openable based on object

  open    : FALSE

methods

  'TRANSPARENT' : open

  'look' : {
    writes "It is presently "
    if open then write "open." else write "closed."
    }

  'look in' :
    if 'TRANSPARENT' -> self then {
      writes main.voice.Who & " look inside ", 'DEF' -> self, ".  "
      'INVENTORY'
      }
    else if not open then
      write "It's not open; ", main.voice.who, " can't look inside."
    else
      write "I can't look inside that."

  'open' :
    if open then
      write main.voice.Who & " would but ", 'DEF' -> self, " is already open."
    else {
      open := TRUE
      write main.voice.Who & " opened ", 'DEF' -> self, "."
      }

  'close' :
    if not open then
      write main.voice.Who & " would but ", 'DEF' -> self, " is already closed."
    else {
      open := FALSE
      write main.voice.Who & " closed ", 'DEF' -> self, "."
      }

end


# Hollow objects.  These are objects that can double as rooms.

class hollow_object based on object

  IsAhollow_object : TRUE

  proximity   : "in"
  visited     : FALSE

  intro       : "I can see"
  empty_intro : "There is nothing of interest " & proximity & " " &
                ('DEF' -> self) & "."

  open        : UNDEFINED
methods

  'ACCESS' : ('ACCESS' --> object) or player.location = self

  'exit' :
    if location then {
      if player.location ~= self then
        write main.voice.WhoIs & " not ", proximity, " ", 'DEF' -> self, "."
      else {
        player.location := location
        'MOVE' -> player
        }
      }
    else
      write main.voice.Who & " can't leave ", 'DEF' -> self, "."

  'leave' : 'exit'

  'look' :
    >>It looks roomy enough to enter.

  'look in' :
    if 'TRANSPARENT' -> self then {
      writes main.voice.Who & " look inside.  "
      'INVENTORY'
      }
    else
      write main.voice.Who & " can't look inside that."

  'enter' :
    if player.location = self then
      write main.voice.WhoIs & " already ", proximity, " ", 'DEF' -> self, "."
    else if location = player.location then {
      if open = FALSE then
        >>It's closed at the moment.
      else {
        player.location := self
        'MOVE' -> player
        }
      }
    else if location = player then
      write main.voice.Who, " cannot enter something that " & main.voice.whois, " carrying."
    else
      write main.voice.Who, " can't enter ", 'DEF' -> self, " because ",
            pronoun, " isn't here."

  'go'    : 'enter'
  'go to' : 'enter'
  'go in' : 'enter'

  'get' :
    if player.location = self then
      write main.voice.Who, " can't pick up something that ", main.voice.whois, " ", proximity, "."
    else
      message --> object

  'BRIEF' :
    write main.voice.WhoIs, " ", proximity, " ", 'DEF' -> self, "."

  'LONGDESC' : 'BRIEF'

# The following is an example of how a kind of multiple inheritance
# can be accomplished in Archetype.  This only "inherits" the messages,
# however, not the attributes.  Be careful to pass only
# non-verb messages to the room object.

  default : {
    if not (message -> compass) and message -> verb_filter then
      ABSENT
    else
      message --> room
   }

end  # hollow_object



# Furniture class.  This is the sort of object that is so much a part
# of the room that it really inherits the exits of the room that it is
# in; it it also possible to "leave" it and remain in the same
# room.

class furniture based on hollow_object

  IsAfurniture : TRUE

  proximity    : "on"

methods

  'TRANSPARENT' : TRUE
  'HORIZON'     : location

  default : {
    if message -> compass then
      message -> location
    else if message -> verb_filter then
      ABSENT
    else
      message --> room
    }

end  # furniture



# container
#
# Simply fill in the "holder" and "contents" attributes with descriptive
# strings; the rest is taken care of.

class container based on object

  IsAcontainer : TRUE

  holder    : "can"
  contents  : "stuff"
  desc      : 'DESC' -> self
  syn       : holder & "|" & contents & " " & holder

  filled    : TRUE

methods

  'look' :
    if filled then
      write "The ", holder, " is full of ", contents, "."
    else
      write "The ", holder, " is empty."

  'DESC' : if filled then
             holder & " of " & contents
           else
             "empty " & holder

end



